plugins {
    id 'java-gradle-plugin'
    id 'groovy'

    id 'eclipse'
    id 'idea'

    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '0.15.0'

    //id 'net.minecrell.licenser' version '0.4.1'
}

group = 'net.minecrell'
version = '3.0.0-SNAPSHOT'
description = 'ForgeGradle 2 extension for Vanilla mods'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

archivesBaseName = project.name.toLowerCase()

repositories {
    jcenter()
    maven {
        name = 'forge'
        url = 'https://files.minecraftforge.net/maven'
    }
}

dependencies {
    implementation localGroovy()
    compileOnly 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
}

//license {
//    include '**/*.java'
//    include '**/*.groovy'
//}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

pluginBundle {
    website = 'https://github.com/Minecrell/VanillaGradle'
    vcsUrl = website
    description = project.description
    tags = ['minecraft', 'mcp', 'sponge']

    plugins {
        serverPlugin {
            id = 'net.minecrell.vanillagradle.server'
            displayName = 'VanillaGradle server plugin'
            description = 'Provides a MCP workspace for the Vanilla server with all run tasks disabled'
        }
        clientPlugin {
            id = 'net.minecrell.vanillagradle.client'
            displayName = 'VanillaGradle client plugin'
            description = 'Provides a MCP workspace for the Vanilla client with all run tasks disabled'
        }

        mergedPlugin {
            id = 'net.minecrell.vanillagradle.merged'
            displayName = 'VanillaGradle merged plugin'
            description = 'Provides a MCP workspace with Vanilla client and server sources merged'
        }
    }

    mavenCoordinates {
        artifactId = project.archivesBaseName
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId project.archivesBaseName

            from components.java

            artifact sourceJar

            repositories {
                maven {
                    url = System.getenv('REPO_' + (version.endsWith('-SNAPSHOT') ? 'SNAPSHOTS' : 'RELEASES')) ?: "$buildDir/repo"
                }
            }
        }
    }
}

wrapper {
    gradleVersion = '7.1.1'
}
